## <summary>Server for managing and downloading certificate revocation lists.</summary>

########################################
## <summary>
##	All of the rules required to
##	administrate an dirmngr environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`dirmngr_admin',`
	gen_require(`
		type dirmngr_t, dirmngr_initrc_exec_t, dirmngr_var_run_t;
		type dirmngr_conf_t, dirmngr_var_lib_t, dirmngr_log_t;
	')

	allow $1 dirmngr_t:process { ptrace signal_perms };
	ps_process_pattern($1, dirmngr_t)

	init_labeled_script_domtrans($1, dirmngr_initrc_exec_t)
	domain_system_change_exemption($1)
	role_transition $2 dirmngr_initrc_exec_t system_r;
	allow $2 system_r;

	files_search_etc($1)
	admin_pattern($1, dirmngr_conf_t)

	logging_search_logs($1)
	admin_pattern($1, dirmngr_log_t)

	files_search_pids($1)
	admin_pattern($1, dirmngr_var_run_t)

	files_search_var_lib($1)
	admin_pattern($1, dirmngr_var_lib_t)
')
