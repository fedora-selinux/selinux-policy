## <summary>Advanced key-value store</summary>

########################################
## <summary>
##	Execute redis server in the redis domin.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
#
interface(`redis_domtrans',`
	gen_require(`
		type redis_t, redis_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, redis_exec_t, redis_t)
')

########################################
## <summary>
##	Execute redis server in the redis domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`redis_initrc_domtrans',`
	gen_require(`
		type redis_initrc_exec_t;
	')

	init_labeled_script_domtrans($1, redis_initrc_exec_t)
')

########################################
## <summary>
##	Read redis's log files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`redis_read_log',`
	gen_require(`
		type redis_log_t;
	')

	logging_search_logs($1)
	read_files_pattern($1, redis_log_t, redis_log_t)
')

########################################
## <summary>
##	Append to redis log files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`redis_append_log',`
	gen_require(`
		type redis_log_t;
	')

	logging_search_logs($1)
	append_files_pattern($1, redis_log_t, redis_log_t)
')

########################################
## <summary>
##	Manage redis log files
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`redis_manage_log',`
	gen_require(`
		type redis_log_t;
	')

	logging_search_logs($1)
	manage_dirs_pattern($1, redis_log_t, redis_log_t)
	manage_files_pattern($1, redis_log_t, redis_log_t)
	manage_lnk_files_pattern($1, redis_log_t, redis_log_t)
')

########################################
## <summary>
##	Search redis lib directories.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`redis_search_lib',`
	gen_require(`
		type redis_var_lib_t;
	')

	allow $1 redis_var_lib_t:dir search_dir_perms;
	files_search_var_lib($1)
')

########################################
## <summary>
##	Read redis lib files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`redis_read_lib_files',`
	gen_require(`
		type redis_var_lib_t;
	')

	files_search_var_lib($1)
	read_files_pattern($1, redis_var_lib_t, redis_var_lib_t)
')

########################################
## <summary>
##	Manage redis lib files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`redis_manage_lib_files',`
	gen_require(`
		type redis_var_lib_t;
	')

	files_search_var_lib($1)
	manage_files_pattern($1, redis_var_lib_t, redis_var_lib_t)
')

########################################
## <summary>
##	Manage redis lib directories.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`redis_manage_lib_dirs',`
	gen_require(`
		type redis_var_lib_t;
	')

	files_search_var_lib($1)
	manage_dirs_pattern($1, redis_var_lib_t, redis_var_lib_t)
')

########################################
## <summary>
##	Read redis PID files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`redis_read_pid_files',`
	gen_require(`
		type redis_var_run_t;
	')

	files_search_pids($1)
	read_files_pattern($1, redis_var_run_t, redis_var_run_t)
')

#######################################
## <summary>
##	Connect to redis over an unix domain stream socket.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`redis_stream_connect',`
	gen_require(`
		type redis_t, redis_var_run_t;
	')

	files_search_pids($1)
	stream_connect_pattern($1, redis_var_run_t, redis_var_run_t, redis_t)
')

########################################
## <summary>
##	Execute redis server in the redis domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`redis_systemctl',`
	gen_require(`
		type redis_t;
		type redis_unit_file_t;
	')

	systemd_exec_systemctl($1)
	init_reload_services($1)
    systemd_read_fifo_file_passwd_run($1)
	allow $1 redis_unit_file_t:file read_file_perms;
	allow $1 redis_unit_file_t:service manage_service_perms;

	ps_process_pattern($1, redis_t)
')

########################################
## <summary>
##	All of the rules required to administrate
##	an redis environment
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`redis_admin',`
	gen_require(`
		type redis_t, redis_initrc_exec_t, redis_var_lib_t;
		type redis_log_t, redis_var_run_t, redis_unit_file_t;
	')

	allow $1 redis_t:process { ptrace signal_perms };
	ps_process_pattern($1, redis_t)

	init_labeled_script_domtrans($1, redis_initrc_exec_t)
	domain_system_change_exemption($1)
	role_transition $2 redis_initrc_exec_t system_r;
	allow $2 system_r;

	logging_search_logs($1)
	admin_pattern($1, redis_log_t)

	files_search_var_lib($1)
	admin_pattern($1, redis_var_lib_t)

	files_search_pids($1)
	admin_pattern($1, redis_var_run_t)

	redis_systemctl($1)
	admin_pattern($1, redis_unit_file_t)
	allow $1 redis_unit_file_t:service all_service_perms;

	optional_policy(`
		systemd_passwd_agent_exec($1)
		systemd_read_fifo_file_passwd_run($1)
	')
')
