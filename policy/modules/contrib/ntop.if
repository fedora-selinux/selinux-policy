## <summary>A network traffic probe similar to the UNIX top command.</summary>

########################################
## <summary>
##	All of the rules required to
##	administrate an ntop environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`ntop_admin',`
	gen_require(`
		type ntop_t, ntop_etc_t, ntop_var_run_t;
		type ntop_initrc_exec_t, ntop_var_lib_t;
	')

	allow $1 ntop_t:process { ptrace signal_perms };
	ps_process_pattern($1, ntop_t)

	init_labeled_script_domtrans($1, ntop_initrc_exec_t)
	domain_system_change_exemption($1)
	role_transition $2 ntop_initrc_exec_t system_r;
	allow $2 system_r;

	files_search_etc($1)
	admin_pattern($1, ntop_etc_t)

	files_search_var_lib($1)
	admin_pattern($1, ntop_var_lib_t)

	files_list_pids($1)
	admin_pattern($1, ntop_var_run_t)
')
