
## <summary>policy for openvswitch</summary>

########################################
## <summary>
##	Execute TEMPLATE in the openvswitch domin.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
#
interface(`openvswitch_domtrans',`
	gen_require(`
		type openvswitch_t, openvswitch_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, openvswitch_exec_t, openvswitch_t)
')
########################################
## <summary>
##	Read openvswitch's log files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`openvswitch_read_log',`
	gen_require(`
		type openvswitch_log_t;
	')

	logging_search_logs($1)
	read_files_pattern($1, openvswitch_log_t, openvswitch_log_t)
')

########################################
## <summary>
##	Append to openvswitch log files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`openvswitch_append_log',`
	gen_require(`
		type openvswitch_log_t;
	')

	logging_search_logs($1)
	append_files_pattern($1, openvswitch_log_t, openvswitch_log_t)
')

########################################
## <summary>
##	Manage openvswitch log files
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`openvswitch_manage_log',`
	gen_require(`
		type openvswitch_log_t;
	')

	logging_search_logs($1)
	manage_dirs_pattern($1, openvswitch_log_t, openvswitch_log_t)
	manage_files_pattern($1, openvswitch_log_t, openvswitch_log_t)
	manage_lnk_files_pattern($1, openvswitch_log_t, openvswitch_log_t)
')

########################################
## <summary>
##	Search openvswitch lib directories.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`openvswitch_search_lib',`
	gen_require(`
		type openvswitch_var_lib_t;
	')

	allow $1 openvswitch_var_lib_t:dir search_dir_perms;
	files_search_var_lib($1)
')

########################################
## <summary>
##	Read openvswitch lib files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`openvswitch_read_lib_files',`
	gen_require(`
		type openvswitch_var_lib_t;
	')

	files_search_var_lib($1)
	read_files_pattern($1, openvswitch_var_lib_t, openvswitch_var_lib_t)
')

########################################
## <summary>
##	Manage openvswitch lib files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`openvswitch_manage_lib_files',`
	gen_require(`
		type openvswitch_var_lib_t;
	')

	files_search_var_lib($1)
	manage_files_pattern($1, openvswitch_var_lib_t, openvswitch_var_lib_t)
')

########################################
## <summary>
##	Manage openvswitch lib directories.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`openvswitch_manage_lib_dirs',`
	gen_require(`
		type openvswitch_var_lib_t;
	')

	files_search_var_lib($1)
	manage_dirs_pattern($1, openvswitch_var_lib_t, openvswitch_var_lib_t)
')

########################################
## <summary>
##	Read openvswitch PID files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`openvswitch_read_pid_files',`
	gen_require(`
		type openvswitch_var_run_t;
	')

	files_search_pids($1)
	read_files_pattern($1, openvswitch_var_run_t, openvswitch_var_run_t)
')

########################################
## <summary>
##	Allow stream connect to openvswitch.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#

interface(`openvswitch_stream_connect',`
    gen_require(`
            type openvswitch_t, openvswitch_var_run_t;
                ')

    files_search_pids($1)
    stream_connect_pattern($1, openvswitch_var_run_t, openvswitch_var_run_t, openvswitch_t)
')

########################################
## <summary>
##	Execute openvswitch server in the openvswitch domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`openvswitch_systemctl',`
	gen_require(`
		type openvswitch_t;
		type openvswitch_unit_file_t;
	')

	systemd_exec_systemctl($1)
	init_reload_services($1)
	allow $1 openvswitch_unit_file_t:file read_file_perms;
	allow $1 openvswitch_unit_file_t:service manage_service_perms;

	ps_process_pattern($1, openvswitch_t)
')


########################################
## <summary>
##	All of the rules required to administrate
##	an openvswitch environment
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`openvswitch_admin',`
	gen_require(`
		type openvswitch_t, openvswitch_log_t, openvswitch_var_lib_t;
		type openvswitch_rw_t, openvswitch_var_run_t, openvswitch_unit_file_t;
	')

	allow $1 openvswitch_t:process { ptrace signal_perms };
	ps_process_pattern($1, openvswitch_t)

	logging_search_logs($1)
	admin_pattern($1, openvswitch_rw_t)

	logging_search_logs($1)
	admin_pattern($1, openvswitch_log_t)

	files_search_var_lib($1)
	admin_pattern($1, openvswitch_var_lib_t)

	files_search_pids($1)
	admin_pattern($1, openvswitch_var_run_t)

	openvswitch_systemctl($1)
	admin_pattern($1, openvswitch_unit_file_t)
	allow $1 openvswitch_unit_file_t:service all_service_perms;
	optional_policy(`
		systemd_passwd_agent_exec($1)
		systemd_read_fifo_file_passwd_run($1)
	')
')
