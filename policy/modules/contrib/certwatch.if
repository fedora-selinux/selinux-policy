## <summary>Digital Certificate Tracking.</summary>

########################################
## <summary>
##	Domain transition to certwatch.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`certwatch_domtrans',`
	gen_require(`
		type certwatch_exec_t, certwatch_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, certwatch_exec_t, certwatch_t)
')

########################################
## <summary>
##	Execute certwatch in the certwatch
##	domain, and allow the specified role
##	the certwatch domain.
##	backchannel.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`certwatch_run',`
	gen_require(`
		attribute_role certwatch_roles;
	')

	certwatch_domtrans($1)
	roleattribute $2 certwatch_roles;
')

########################################
## <summary>
##	Execute certwatch in the certwatch domain, and
##	allow the specified role the certwatch domain,
##	and use the caller's terminal. Has a sigchld
##	backchannel.  (Deprecated)
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <param name="terminal">
##	<summary>
##	The type of the terminal allow the certwatch domain to use.
##	</summary>
## </param>
## <rolecap/>
#
interface(`certwatach_run',`
	refpolicywarn(`$0($*) has been deprecated, please use certwatch_run() instead.')
	certwatch_run($*)
')
