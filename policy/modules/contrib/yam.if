## <summary>Yum/Apt Mirroring.</summary>

########################################
## <summary>
##	Execute yam in the yam domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`yam_domtrans',`
	gen_require(`
		type yam_t, yam_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, yam_exec_t, yam_t)
')

########################################
## <summary>
##	Execute yam in the yam domain, and
##	allow the specified role the yam domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`yam_run',`
	gen_require(`
		attribute_role yam_roles;
	')

	yam_domtrans($1)
	roleattribute $2 yam_roles;
')

########################################
## <summary>
##	Read yam content.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`yam_read_content',`
	gen_require(`
		type yam_content_t;
	')

	allow $1 yam_content_t:dir list_dir_perms;
	read_files_pattern($1, yam_content_t, yam_content_t)
	read_lnk_files_pattern($1, yam_content_t, yam_content_t)
')
