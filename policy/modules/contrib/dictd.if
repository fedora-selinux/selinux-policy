## <summary>Dictionary daemon.</summary>

########################################
## <summary>
##	Use dictionary services by connecting
##	over TCP.  (Deprecated)
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`dictd_tcp_connect',`
	refpolicywarn(`$0($*) has been deprecated.')
')

########################################
## <summary>
##	All of the rules required to
##	administrate an dictd environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`dictd_admin',`
	gen_require(`
		type dictd_t, dictd_etc_t, dictd_var_lib_t;
		type dictd_var_run_t, dictd_initrc_exec_t;
	')

	allow $1 dictd_t:process signal_perms;
	ps_process_pattern($1, dictd_t)
	tunable_policy(`deny_ptrace',`',`
		allow $1 dictd_t:process ptrace;
	')

	init_labeled_script_domtrans($1, dictd_initrc_exec_t)
	domain_system_change_exemption($1)
	role_transition $2 dictd_initrc_exec_t system_r;
	allow $2 system_r;

	files_list_etc($1)
	admin_pattern($1, dictd_etc_t)

	files_list_var_lib($1)
	admin_pattern($1, dictd_var_lib_t)

	files_list_pids($1)
	admin_pattern($1, dictd_var_run_t)
')
