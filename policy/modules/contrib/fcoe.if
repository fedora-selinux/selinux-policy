## <summary>Fibre Channel over Ethernet utilities.</summary>

#######################################
## <summary>
##	Send to fcoemon with a unix dgram socket.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`fcoe_dgram_send_fcoemon',`
	gen_require(`
		type fcoemon_t, fcoemon_var_run_t;
	')

	files_search_pids($1)
	dgram_send_pattern($1, fcoemon_var_run_t, fcoemon_var_run_t, fcoemon_t)
')

########################################
## <summary>
##	All of the rules required to
##	administrate an fcoemon environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`fcoe_admin',`
	gen_require(`
		type fcoemon_t, fcoemon_initrc_exec_t, fcoemon_var_run_t;
	')

	allow $1 fcoemon_t:process { ptrace signal_perms };
	ps_process_pattern($1, fcoemon_t)

	init_labeled_script_domtrans($1, fcoemon_initrc_exec_t)
	domain_system_change_exemption($1)
	role_transition $2 fcoemon_initrc_exec_t system_r;
	allow $2 system_r;

	files_search_pids($1)
	admin_pattern($1, fcoemon_var_run_t)
')
