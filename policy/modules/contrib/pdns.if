## <summary>PowerDNS DNS server.</summary>

########################################
## <summary>
##      Execute pdns in the pdns domain.
## </summary>
## <param name="domain">
##      <summary>
##      Domain allowed to transition.
##      </summary>
## </param>
#
interface(`pdns_domtrans',`
	gen_require(`
		type pdns_t, pdns_exec_t;
	')

	domtrans_pattern($1, pdns_exec_t, pdns_t)
')

########################################
## <summary>
##      Execute pdns_control in the pdns_control domain.
## </summary>
## <param name="domain">
##      <summary>
##      Domain allowed to transition.
##      </summary>
## </param>
#
interface(`pdns_domtrans_pdns_control',`
        gen_require(`
                type pdns_control_t, pdns_control_exec_t;
        ')

        corecmd_search_bin($1)
        domtrans_pattern($1, pdns_control_exec_t, pdns_control_t)
')

########################################
## <summary>
##      Allow the specified domain to read
##      pdns configuration files.
## </summary>
## <param name="domain">
##      <summary>
##      Domain allowed access.
##      </summary>
## </param>
## <rolecap/>
#
interface(`pdns_read_config',`
        gen_require(`
                type pdns_conf_t;
        ')

        files_search_etc($1)
        allow $1 pdns_conf_t:dir list_dir_perms;
        read_files_pattern($1, pdns_conf_t, pdns_conf_t)
        read_lnk_files_pattern($1, pdns_conf_t, pdns_conf_t)
')

########################################
## <summary>
##	Connect to pdns over an unix
##	stream socket.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`pdns_stream_connect',`
	gen_require(`
		type pdns_t, pdns_var_run_t;
	')

	files_search_pids($1)
	stream_connect_pattern($1, pdns_var_run_t, pdns_var_run_t, pdns_t)
')
