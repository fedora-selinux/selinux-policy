## <summary>Statistics collection daemon for filling RRD files.</summary>

########################################
## <summary>
##	Transition to collectd.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
#
interface(`collectd_domtrans',`
	gen_require(`
		type collectd_t, collectd_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, collectd_exec_t, collectd_t)
')

########################################
## <summary>
##	Execute collectd server in the collectd domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`collectd_initrc_domtrans',`
	gen_require(`
		type collectd_initrc_exec_t;
	')

	init_labeled_script_domtrans($1, collectd_initrc_exec_t)
')

########################################
## <summary>
##	Search collectd lib directories.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`collectd_search_lib',`
	gen_require(`
		type collectd_var_lib_t;
	')

	allow $1 collectd_var_lib_t:dir search_dir_perms;
	files_search_var_lib($1)
')

########################################
## <summary>
##	Read collectd lib files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`collectd_read_lib_files',`
	gen_require(`
		type collectd_var_lib_t;
	')

	files_search_var_lib($1)
	read_files_pattern($1, collectd_var_lib_t, collectd_var_lib_t)
')

########################################
## <summary>
##	Manage collectd lib files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`collectd_manage_lib_files',`
	gen_require(`
		type collectd_var_lib_t;
	')

	files_search_var_lib($1)
	manage_files_pattern($1, collectd_var_lib_t, collectd_var_lib_t)
')

########################################
## <summary>
##	Manage collectd lib directories.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`collectd_manage_lib_dirs',`
	gen_require(`
		type collectd_var_lib_t;
	')

	files_search_var_lib($1)
	manage_dirs_pattern($1, collectd_var_lib_t, collectd_var_lib_t)
')

########################################
## <summary>
##	Manage collectd httpd rw content.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`collectd_manage_rw_content',`
	gen_require(`
		type collectd_rw_content_t;
	')

	manage_dirs_pattern($1, collectd_rw_content_t, collectd_rw_content_t)
	manage_files_pattern($1, collectd_rw_content_t, collectd_rw_content_t)
	manage_lnk_files_pattern($1, collectd_rw_content_t, collectd_rw_content_t)
')

########################################
## <summary>
##	Execute collectd server in the collectd domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`collectd_systemctl',`
	gen_require(`
		type collectd_t;
		type collectd_unit_file_t;
	')

	systemd_exec_systemctl($1)
	init_reload_services($1)
	allow $1 collectd_unit_file_t:file read_file_perms;
	allow $1 collectd_unit_file_t:service manage_service_perms;

	ps_process_pattern($1, collectd_t)
')

########################################
## <summary>
##	All of the rules required to administrate
##	an collectd environment
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`collectd_admin',`
	gen_require(`
		type collectd_t, collectd_initrc_exec_t, collectd_var_run_t;
        type collectd_var_lib_t, collectd_unit_file_t;
	')

	allow $1 collectd_t:process signal_perms;
	ps_process_pattern($1, collectd_t)

	tunable_policy(`deny_ptrace',`',`
		allow $1 collectd_t:process ptrace;
	')

	collectd_initrc_domtrans($1)
	domain_system_change_exemption($1)
	role_transition $2 collectd_initrc_exec_t system_r;
	allow $2 system_r;

	files_search_pids($1)
	admin_pattern($1, collectd_var_run_t)

	files_search_var_lib($1)
	admin_pattern($1, collectd_var_lib_t)

	collectd_systemctl($1)
	admin_pattern($1, collectd_unit_file_t)
	allow $1 collectd_unit_file_t:service all_service_perms;
')

