## <summary>Scalable, high-performance, open source NoSQL database.</summary>

########################################
## <summary>
##	All of the rules required to
##	administrate an mongodb environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`mongodb_admin',`
	gen_require(`
		type mongod_t, mongod_initrc_exec_t, mongod_log_t;
		type mongod_var_lib_t, mongod_var_run_t;
	')

	allow $1 mongod_t:process { ptrace signal_perms };
	ps_process_pattern($1, mongod_t)

	init_labeled_script_domtrans($1, mongod_initrc_exec_t)
	domain_system_change_exemption($1)
	role_transition $2 mongod_initrc_exec_t system_r;
	allow $2 system_r;

	logging_search_logs($1)
	admin_pattern($1, mongod_log_t)

	files_search_var_lib($1)
	admin_pattern($1, mongod_var_lib_t)

	files_search_pids($1)
	admin_pattern($1, mongod_var_run_t)
')
