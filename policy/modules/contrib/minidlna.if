## <summary>MiniDLNA lightweight DLNA/UPnP media server</summary>

########################################
## <summary>
##	All of the rules required to
##	administrate an minidlna environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`minidlna_admin',`
	gen_require(`
		type minidlna_t, minidlna_var_run_t, minidlna_initrc_exec_t;
		type minidlna_conf_t, minidlna_log_t, minidlna_db_t;
	')

	allow $1 minidlna_t:process { ptrace signal_perms };
	ps_process_pattern($1, minidlna_t)

	minidlna_initrc_domtrans($1)
	domain_system_change_exemption($1)
	role_transition $2 minidlna_initrc_exec_t system_r;
	allow $2 system_r;

	files_search_etc($1)
	admin_pattern($1, minidlna_conf_t)

	logging_search_logs($1)
	admin_pattern($1, minidlna_log_t)

	files_search_var_lib($1)
	admin_pattern($1, minidlna_db_t)

	files_search_pids($1)
	admin_pattern($1, minidlna_var_run_t)
')

########################################
## <summary>
##	Execute minidlna init scripts in
##	the initrc domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`minidlna_initrc_domtrans',`
	gen_require(`
		type minidlna_initrc_exec_t;
	')

	init_labeled_script_domtrans($1, minidlna_initrc_exec_t)
')
