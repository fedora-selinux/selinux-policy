## <summary>Dump topology and locality information from hardware tables.</summary>

########################################
## <summary>
##	Execute hwloc dhwd in the hwloc dhwd domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`hwloc_domtrans_dhwd',`
	gen_require(`
		type hwloc_dhwd_t, hwloc_dhwd_exec_t;
	')

	domtrans_pattern($1, hwloc_dhwd_exec_t, hwloc_dhwd_t)
')

########################################
## <summary>
##	Execute hwloc dhwd in the hwloc dhwd domain, and
##	allow the specified role the hwloc dhwd domain,
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`hwloc_run_dhwd',`
	gen_require(`
		attribute_role hwloc_dhwd_roles;
	')

	hwloc_domtrans_dhwd($1)
	roleattribute $2 hwloc_dhwd_roles;
')

########################################
## <summary>
##	Execute hwloc dhwd in the caller domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`hwloc_exec_dhwd',`
	gen_require(`
		type hwloc_dhwd_exec_t;
	')

	can_exec($1, hwloc_dhwd_exec_t)
')

########################################
## <summary>
##	Read hwloc runtime files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`hwloc_read_runtime_files',`
	gen_require(`
		type hwloc_var_run_t;
	')

	files_search_pids($1)
	read_files_pattern($1, hwloc_var_run_t, hwloc_var_run_t)
')

########################################
## <summary>
##	All of the rules required to
##	administrate an hwloc environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`hwloc_admin',`
	gen_require(`
		type hwloc_dhwd_t, hwloc_var_run_t;
	')

	allow $1 hwloc_dhwd_t:process { signal_perms };
	ps_process_pattern($1, hwloc_dhwd_t)

	tunable_policy(`deny_ptrace',`',`
		allow $1 hwloc_dhwd_t:process ptrace;
	')

	admin_pattern($1, hwloc_var_run_t)
	files_pid_filetrans($1, hwloc_var_run_t, dir, "hwloc")
')
