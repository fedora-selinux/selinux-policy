## <summary>Concurrent versions system.</summary>

######################################
## <summary>
##  Dontaudit Attempts to list the CVS data and metadata.
## </summary>
## <param name="domain">
##  <summary>
##	Domain to not audit.
##  </summary>
## </param>
#
interface(`cvs_dontaudit_list_data',`
    gen_require(`
        type cvs_data_t;
    ')

    dontaudit $1 cvs_data_t:dir list_dir_perms;
')

########################################
## <summary>
##	Read CVS data and metadata content.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`cvs_read_data',`
	gen_require(`
		type cvs_data_t;
	')

	list_dirs_pattern($1, cvs_data_t, cvs_data_t)
	read_files_pattern($1, cvs_data_t, cvs_data_t)
	read_lnk_files_pattern($1, cvs_data_t, cvs_data_t)
')

########################################
## <summary>
##	Execute cvs in the caller domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`cvs_exec',`
	gen_require(`
		type cvs_exec_t;
	')

	corecmd_search_bin($1)
	can_exec($1, cvs_exec_t)
')

########################################
## <summary>
##	Transition to cvs named content
## </summary>
## <param name="domain">
##	<summary>
##      Domain allowed access.
##	</summary>
## </param>
#
interface(`cvs_filetrans_home_content',`
	gen_require(`
		type cvs_home_t;
	')

	userdom_user_home_dir_filetrans($1, cvs_home_t, file, ".cvsignore")
')

########################################
## <summary>
##	All of the rules required to
##	administrate an cvs environment
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`cvs_admin',`
	gen_require(`
		type cvs_t, cvs_tmp_t, cvs_initrc_exec_t;
		type cvs_data_t, cvs_var_run_t, cvs_keytab_t;
		type cvs_home_t;
	')

	allow $1 cvs_t:process signal_perms;
	ps_process_pattern($1, cvs_t)

	tunable_policy(`deny_ptrace',`',`
		allow $1 cvs_t:process ptrace;
	')

	# Allow cvs_t to restart the apache service
	init_labeled_script_domtrans($1, cvs_initrc_exec_t)
	domain_system_change_exemption($1)
	role_transition $2 cvs_initrc_exec_t system_r;
	allow $2 system_r;

	files_search_etc($1)
	admin_pattern($1, cvs_keytab_t)

	files_list_tmp($1)
	admin_pattern($1, cvs_tmp_t)

	files_search_usr($1)
	admin_pattern($1, cvs_data_t)

	files_list_pids($1)
	admin_pattern($1, cvs_var_run_t)

	userdom_search_user_home_dirs($1)
	admin_pattern($1, cvs_home_t)
')
