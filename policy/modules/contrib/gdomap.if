## <summary>GNUstep distributed object mapper.</summary>

########################################
## <summary>
##	Read gdomap configuration files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`gdomap_read_config',`
	gen_require(`
		type gdomap_conf_t;
	')

	files_search_etc($1)
	allow $1 gdomap_conf_t:file read_file_perms;
')

########################################
## <summary>
##	All of the rules required to
##	administrate an gdomap environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`gdomap_admin',`
	gen_require(`
		type gdomap_t, gdomap_conf_t, gdomap_initrc_exec_t;
		type gdomap_var_run_t;
	')

	allow $1 gdomap_t:process { ptrace signal_perms };
	ps_process_pattern($1, gdomap_t)

	init_labeled_script_domtrans($1, gdomap_initrc_exec_t)
	domain_system_change_exemption($1)
	role_transition $2 gdomap_initrc_exec_t system_r;
	allow $2 system_r;

	files_search_etc($1)
	admin_pattern($1, gdomap_conf_t)

	files_search_pids($1)
	admin_pattern($1, gdomap_var_run_t)
')
