## <summary>Cyphesis WorldForge game server.</summary>

########################################
## <summary>
##	Execute a domain transition to run cyphesis.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
#
interface(`cyphesis_domtrans',`
	gen_require(`
		type cyphesis_t, cyphesis_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, cyphesis_exec_t, cyphesis_t)
')

########################################
## <summary>
##	All of the rules required to
##	administrate an cyphesis environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`cyphesis_admin',`
	gen_require(`
		type cyphesis_t, cyphesis_initrc_exec_t, cyphesis_log_t;
		type cyphesis_var_run_t, cyphesis_tmp_t;
	')

	allow $1 cyphesis_t:process { ptrace signal_perms };
	ps_process_pattern($1, cyphesis_t)

	init_labeled_script_domtrans($1, cyphesis_initrc_exec_t)
	domain_system_change_exemption($1)
	role_transition $2 cyphesis_initrc_exec_t system_r;
	allow $2 system_r;

	logging_search_logs($1)
	admin_pattern($1, cyphesis_log_t)

	files_search_pids($1)
	admin_pattern($1, cyphesis_var_run_t)

	files_search_tmp($1)
	admin_pattern($1, cyphesis_tmp_t)
')
