## <summary>Resource management daemon.</summary>

########################################
## <summary>
##	Connect to resmgrd over a unix domain
##	stream socket.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`resmgr_stream_connect',`
	gen_require(`
		type resmgrd_var_run_t, resmgrd_t;
	')

	files_search_pids($1)
	stream_connect_pattern($1, resmgrd_var_run_t, resmgrd_var_run_t, resmgrd_t)
')

########################################
## <summary>
##	All of the rules required to
##	administrate an resmgr environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`resmgr_admin',`
	gen_require(`
		type resmgrd_t, resmgrd_initrc_exec_t, resmgrd_var_run_t;
		type resmgrd_etc_t;
	')

	allow $1 resmgrd_t:process { ptrace signal_perms };
	ps_process_pattern($1, resmgrd_t)

	init_labeled_script_domtrans($1, resmgrd_initrc_exec_t)
	domain_system_change_exemption($1)
	role_transition $2 resmgrd_initrc_exec_t system_r;
	allow $2 system_r;

	files_search_etc($1)
	admin_pattern($1, resmgrd_etc_t)

	files_search_pids($1)
	admin_pattern($1, resmgrd_var_run_t)
')
