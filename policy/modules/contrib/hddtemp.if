## <summary>Hard disk temperature tool running as a daemon.</summary>

#######################################
## <summary>
##	Execute a domain transition to run hddtemp.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`hddtemp_domtrans',`
	gen_require(`
		type hddtemp_t, hddtemp_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, hddtemp_exec_t, hddtemp_t)
')

########################################
## <summary>
##	Execute hddtemp in the hddtemp domain, and
##	allow the specified role the hddtemp domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
#
interface(`hddtemp_run',`
	gen_require(`
		type hddtemp_t;
		attribute_role hddtemp_roles;
	')

    hddtemp_domtrans($1)
    roleattribute $2 hddtemp_roles;
')

######################################
## <summary>
##	Execute hddtemp in the caller domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`hddtemp_exec',`
	gen_require(`
		type hddtemp_exec_t;
	')

	corecmd_search_bin($1)
	can_exec($1, hddtemp_exec_t)
')

########################################
## <summary>
##	All of the rules required to
##	administrate an hddtemp environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`hddtemp_admin',`
	gen_require(`
		type hddtemp_t, hddtemp_etc_t, hddtemp_initrc_exec_t;
	')

	allow $1 hddtemp_t:process signal_perms;
	ps_process_pattern($1, hddtemp_t)

	tunable_policy(`deny_ptrace',`',`
		allow $1 hddtemp_t:process ptrace;
	')

	init_labeled_script_domtrans($1, hddtemp_initrc_exec_t)
	domain_system_change_exemption($1)
	role_transition $2 hddtemp_initrc_exec_t system_r;
	allow $2 system_r;

	admin_pattern($1, hddtemp_etc_t)
	files_search_etc($1)
')
