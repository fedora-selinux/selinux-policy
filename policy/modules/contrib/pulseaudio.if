## <summary>Pulseaudio network sound server.</summary>

########################################
## <summary>
##	Role access for pulseaudio
## </summary>
## <param name="role">
##	<summary>
##	Role allowed access
##	</summary>
## </param>
## <param name="domain">
##	<summary>
##	User domain for the role
##	</summary>
## </param>
#
interface(`pulseaudio_role',`
	gen_require(`
        attribute pulseaudio_tmpfsfile;
		type pulseaudio_t, pulseaudio_exec_t, pulseaudio_tmpfs_t;
		class dbus { acquire_svc send_msg };
	')

	role $1 types pulseaudio_t;

	# Transition from the user domain to the derived domain.
	domtrans_pattern($2, pulseaudio_exec_t, pulseaudio_t)

	ps_process_pattern($2, pulseaudio_t)

	allow pulseaudio_t $2:process { signal signull };
	allow $2 pulseaudio_t:process { signal signull sigkill };
	allow $2 pulseaudio_t:process2 nnp_transition;
	ps_process_pattern(pulseaudio_t, $2)

	allow pulseaudio_t $2:unix_stream_socket connectto;
	allow $2 pulseaudio_t:unix_stream_socket connectto;

	allow $2 { pulseaudio_tmpfs_t pulseaudio_tmpfsfile }:dir { manage_dir_perms relabel_dir_perms };
	allow $2 { pulseaudio_tmpfs_t pulseaudio_tmpfsfile }:file { manage_file_perms relabel_file_perms };

	userdom_manage_tmp_role($1, pulseaudio_t)

	allow $2 pulseaudio_t:dbus send_msg;
	allow pulseaudio_t $2:dbus { acquire_svc send_msg };
')

########################################
## <summary>
##	Execute a domain transition to run pulseaudio.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
#
interface(`pulseaudio_domtrans',`
	gen_require(`
		attribute pulseaudio_client;
		type pulseaudio_t, pulseaudio_exec_t;
	')

	typeattribute $1 pulseaudio_client;

	corecmd_search_bin($1)
	domtrans_pattern($1, pulseaudio_exec_t, pulseaudio_t)
')

########################################
## <summary>
##	Execute pulseaudio in the pulseaudio domain, and
##	allow the specified role the pulseaudio domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
#
interface(`pulseaudio_run',`
	gen_require(`
		type pulseaudio_t;
	')

	pulseaudio_domtrans($1)
	role $2 types pulseaudio_t;
')

########################################
## <summary>
##	Execute a pulseaudio in the current domain.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed access.
## </summary>
## </param>
#
interface(`pulseaudio_exec',`
	gen_require(`
		type pulseaudio_exec_t;
	')

	can_exec($1, pulseaudio_exec_t)
')

########################################
## <summary>
##	Do not audit to execute a pulseaudio.
## </summary>
## <param name="domain">
## <summary>
##	Domain to not audit.
## </summary>
## </param>
#
interface(`pulseaudio_dontaudit_exec',`
	gen_require(`
		type pulseaudio_exec_t;
	')

	dontaudit $1 pulseaudio_exec_t:file exec_file_perms;
')

########################################
## <summary>
##	Send signull signal to pulseaudio
##	processes.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`pulseaudio_signull',`
	gen_require(`
		type pulseaudio_t;
	')

	allow $1 pulseaudio_t:process signull;
')

#####################################
## <summary>
##	Connect to pulseaudio over a unix domain
##	stream socket.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`pulseaudio_stream_connect',`
	gen_require(`
		type pulseaudio_t, pulseaudio_var_run_t;
		type pulseaudio_home_t;
	')

	files_search_pids($1)
	allow $1 pulseaudio_t:process signull;
	allow pulseaudio_t $1:process signull;
	allow $1 pulseaudio_t:unix_stream_socket create_stream_socket_perms;
	stream_connect_pattern($1, pulseaudio_var_run_t, pulseaudio_var_run_t, pulseaudio_t)
	stream_connect_pattern($1, pulseaudio_home_t, pulseaudio_home_t, pulseaudio_t)
')

########################################
## <summary>
##	Send and receive messages from
##	pulseaudio over dbus.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`pulseaudio_dbus_chat',`
	gen_require(`
		type pulseaudio_t;
		class dbus send_msg;
	')

	allow $1 pulseaudio_t:dbus send_msg;
	allow pulseaudio_t $1:dbus send_msg;
')

########################################
## <summary>
##	Set the attributes of the pulseaudio homedir.
## </summary>
## <param name="user_domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`pulseaudio_setattr_home_dir',`
	gen_require(`
		type pulseaudio_home_t;
	')

	allow $1 pulseaudio_home_t:dir setattr;
')

########################################
## <summary>
##	Read pulseaudio homedir files.
## </summary>
## <param name="user_domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`pulseaudio_read_home_files',`
	gen_require(`
		type pulseaudio_home_t;
	')

	userdom_search_user_home_dirs($1)
	read_files_pattern($1, pulseaudio_home_t, pulseaudio_home_t)
	read_lnk_files_pattern($1, pulseaudio_home_t, pulseaudio_home_t)
')

########################################
## <summary>
##	Read and write Pulse Audio files.
## </summary>
## <param name="user_domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`pulseaudio_rw_home_files',`
	gen_require(`
		type pulseaudio_home_t;
	')

	rw_files_pattern($1, pulseaudio_home_t, pulseaudio_home_t)
	read_lnk_files_pattern($1, pulseaudio_home_t, pulseaudio_home_t)
	userdom_search_user_home_dirs($1)
')

########################################
## <summary>
##	Create, read, write, and delete pulseaudio
##	home directories.
## </summary>
## <param name="user_domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`pulseaudio_manage_home_dirs',`
	gen_require(`
		type pulseaudio_home_t;
	')

	userdom_search_user_home_dirs($1)
	manage_dirs_pattern($1, pulseaudio_home_t, pulseaudio_home_t)
')

########################################
## <summary>
##	Create, read, write, and delete pulseaudio
##	home directory files.
## </summary>
## <param name="user_domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`pulseaudio_manage_home_files',`
	gen_require(`
		type pulseaudio_home_t;
	')

	userdom_search_user_home_dirs($1)
	manage_files_pattern($1, pulseaudio_home_t, pulseaudio_home_t)
	read_lnk_files_pattern($1, pulseaudio_home_t, pulseaudio_home_t)
	pulseaudio_filetrans_home_content($1)
')

########################################
## <summary>
##	Create, read, write, and delete pulseaudio
##	home directory symlinks.
## </summary>
## <param name="user_domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`pulseaudio_manage_home_symlinks',`
	gen_require(`
		type pulseaudio_home_t;
	')

	userdom_search_user_home_dirs($1)
	manage_lnk_files_pattern($1, pulseaudio_home_t, pulseaudio_home_t)
')

########################################
## <summary>
##	Create pulseaudio content in the user home directory
##	with an correct label.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`pulseaudio_filetrans_home_content',`
	gen_require(`
		type pulseaudio_home_t;
	')

	userdom_user_home_dir_filetrans($1, pulseaudio_home_t, dir, ".pulse")
	userdom_user_home_dir_filetrans($1, pulseaudio_home_t, file, ".pulse-cookie")
	userdom_user_home_dir_filetrans($1, pulseaudio_home_t, file, ".esd_auth")
	optional_policy(`
		gnome_config_filetrans($1, pulseaudio_home_t, dir, "pulse")
	')
')

########################################
## <summary>
##	Create pulseaudio content in the admin home directory
##	with an correct label.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`pulseaudio_filetrans_admin_home_content',`
	gen_require(`
		type pulseaudio_home_t;
	')

	userdom_admin_home_dir_filetrans($1, pulseaudio_home_t, dir, ".pulse")
	userdom_admin_home_dir_filetrans($1, pulseaudio_home_t, file, ".pulse-cookie")
	userdom_admin_home_dir_filetrans($1, pulseaudio_home_t, file, ".esd_auth")
')

#######################################
## <summary>
##  Make the specified tmpfs file type
##  pulseaudio tmpfs content.
## </summary>
## <param name="file_type">
##  <summary>
##  File type to make pulseaudio tmpfs content.
##  </summary>
## </param>
#
interface(`pulseaudio_tmpfs_content',`
    gen_require(`
        attribute pulseaudio_tmpfsfile;
    ')

    typeattribute $1 pulseaudio_tmpfsfile;
')

########################################
## <summary>
##	Allow the domain to read pulseaudio state files in /proc.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`pulseaudio_read_state',`
	gen_require(`
		type pulseaudio_t;
	')

	kernel_search_proc($1)
	ps_process_pattern($1, pulseaudio_t)
')
