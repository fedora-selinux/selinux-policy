## <summary>Abstract Machine Test Utility.</summary>

########################################
## <summary>
##	Execute a domain transition to run Amtu.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`amtu_domtrans',`
	gen_require(`
		type amtu_t, amtu_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, amtu_exec_t, amtu_t)
')

########################################
## <summary>
##	Execute a domain transition to run
##	Amtu, and allow the specified role
##	the Amtu domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
#
interface(`amtu_run',`
	gen_require(`
		attribute_role amtu_roles;
	')

	amtu_domtrans($1)
	roleattribute $2 amtu_roles;
')

########################################
## <summary>
##	All of the rules required to
##	administrate an amtu environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`amtu_admin',`
	gen_require(`
		type amtu_t, amtu_initrc_exec_t;
	')

	allow $1 amtu_t:process { ptrace signal_perms };
	ps_process_pattern($1, amtu_t)

	init_labeled_script_domtrans($1, amtu_initrc_exec_t)
	domain_system_change_exemption($1)
	role_transition $2 amtu_initrc_exec_t system_r;
	allow $2 system_r;
')
