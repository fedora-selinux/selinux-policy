## <summary>The onion router.</summary>

########################################
## <summary>
##	Execute a domain transition to run tor.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`tor_domtrans',`
	gen_require(`
		type tor_t, tor_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, tor_exec_t, tor_t)
')

#######################################
## <summary>
##      Execute tor server in the tor domain.
## </summary>
## <param name="domain">
##      <summary>
##      Domain allowed to transition.
##      </summary>
## </param>
#
interface(`tor_systemctl',`
        gen_require(`
                type tor_t;
                type tor_unit_file_t;
        ')

        systemd_exec_systemctl($1)
	init_reload_services($1)
        allow $1 tor_unit_file_t:file read_file_perms;
        allow $1 tor_unit_file_t:service manage_service_perms;

        ps_process_pattern($1, tor_t)
')

########################################
## <summary>
##	All of the rules required to
##	administrate an tor environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`tor_admin',`
	gen_require(`
		type tor_t, tor_var_log_t, tor_etc_t;
		type tor_var_lib_t, tor_var_run_t;
		type tor_initrc_exec_t;
		type tor_unit_file_t;
	')

	allow $1 tor_t:process signal_perms;
	ps_process_pattern($1, tor_t)

	tunable_policy(`deny_ptrace',`',`
		allow $1 tor_t:process ptrace;
	')

	init_labeled_script_domtrans($1, tor_initrc_exec_t)
	domain_system_change_exemption($1)
	role_transition $2 tor_initrc_exec_t system_r;
	allow $2 system_r;

	files_list_etc($1)
	admin_pattern($1, tor_etc_t)

	files_list_var_lib($1)
	admin_pattern($1, tor_var_lib_t)

	logging_list_logs($1)
	admin_pattern($1, tor_var_log_t)

	files_list_pids($1)
	admin_pattern($1, tor_var_run_t)

	tor_systemctl($1)
	admin_pattern($1, tor_unit_file_t)
	allow $1 tor_unit_file_t:service all_service_perms;

	optional_policy(`
		systemd_passwd_agent_exec($1)
		systemd_read_fifo_file_passwd_run($1)
	')
')
