## <summary>Pingd of the Whatsup cluster node up/down detection utility.</summary>

########################################
## <summary>
##	Execute a domain transition to run pingd.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
#
interface(`pingd_domtrans',`
	gen_require(`
		type pingd_t, pingd_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, pingd_exec_t, pingd_t)
')

#######################################
## <summary>
##	Read pingd etc configuration files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`pingd_read_config',`
	gen_require(`
		type pingd_etc_t;
	')

	files_search_etc($1)
	allow $1 pingd_etc_t:file read_file_perms;
')

#######################################
## <summary>
##	Create, read, write, and delete
##	pingd etc configuration files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`pingd_manage_config',`
	gen_require(`
		type pingd_etc_t;
	')

	files_search_etc($1)
	manage_dirs_pattern($1, pingd_etc_t, pingd_etc_t)
	manage_files_pattern($1, pingd_etc_t, pingd_etc_t)
')

#######################################
## <summary>
##	All of the rules required to
##	administrate an pingd environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`pingd_admin',`
	gen_require(`
		type pingd_t, pingd_etc_t, pingd_modules_t;
		type pingd_initrc_exec_t;
	')

	allow $1 pingd_t:process signal_perms;
	ps_process_pattern($1, pingd_t)

	tunable_policy(`deny_ptrace',`',`
		allow $1 pingd_t:process ptrace;
	')

	init_labeled_script_domtrans($1, pingd_initrc_exec_t)
	domain_system_change_exemption($1)
	role_transition $2 pingd_initrc_exec_t system_r;
	allow $2 system_r;

	files_list_etc($1)
	admin_pattern($1, pingd_etc_t)

	files_list_usr($1)
	admin_pattern($1, pingd_modules_t)
')
