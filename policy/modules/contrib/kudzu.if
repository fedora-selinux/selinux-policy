## <summary>Hardware detection and configuration tools.</summary>

########################################
## <summary>
##	Execute kudzu in the kudzu domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`kudzu_domtrans',`
	gen_require(`
		type kudzu_t, kudzu_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, kudzu_exec_t, kudzu_t)
')

########################################
## <summary>
##	Execute kudzu in the kudzu domain, and
##	allow the specified role the kudzu domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`kudzu_run',`
	gen_require(`
		attribute_role kudzu_roles;
	')

	kudzu_domtrans($1)
	roleattribute $2 kudzu_roles;
')

########################################
## <summary>
##	Get attributes of kudzu executable files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`kudzu_getattr_exec_files',`
	gen_require(`
		type kudzu_exec_t;
	')

	allow $1 kudzu_exec_t:file getattr_file_perms;
')

########################################
## <summary>
##	All of the rules required to
##	administrate an kudzu environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`kudzu_admin',`
	gen_require(`
		type kudzu_t, kudzu_initrc_exec_t, kudzu_var_run_t;
		type kudzu_tmp_t;
	')

	allow $1 kudzu_t:process { signal_perms };
	ps_process_pattern($1, kudzu_t)

	tunable_policy(`deny_ptrace',`',`
		allow $1 kudzu_t:process ptrace;
	')

	init_labeled_script_domtrans($1, kudzu_initrc_exec_t)
	domain_system_change_exemption($1)
	role_transition $2 kudzu_initrc_exec_t system_r;
	allow $2 system_r;

	files_search_tmp($1)
	admin_pattern($1, kudzu_tmp_t)

	files_search_pids($1)
	admin_pattern($1, kudzu_var_run_t)

	kudzu_run($1, $2)
')
