## <summary>Establish connections to iSCSI devices.</summary>

########################################
## <summary>
##	Execute a domain transition to run iscsid.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
#
interface(`iscsid_domtrans',`
	gen_require(`
		type iscsid_t, iscsid_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, iscsid_exec_t, iscsid_t)
')

########################################
## <summary>
##	Execute iscsid programs in the iscsid domain.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
## <param name="role">
##  <summary>
##  The role to allow the iscsid domain.
##  </summary>
## </param>
## <rolecap/>
#
interface(`iscsid_run',`
	gen_require(`
		attribute_role iscsid_roles;
	')

    iscsid_domtrans($1)
    roleattribute $2 iscsid_roles;
')

########################################
## <summary>
##	Create, read, write, and delete
##	iscsid lock files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`iscsi_manage_lock',`
	gen_require(`
		type iscsi_lock_t;
	')

    files_search_locks($1)
    manage_files_pattern($1, iscsi_lock_t, iscsi_lock_t)
    manage_dirs_pattern($1, iscsi_lock_t, iscsi_lock_t)
')

########################################
## <summary>
##	Create, read, write, and delete
##	iscsid sempaphores.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`iscsi_manage_semaphores',`
	gen_require(`
		type iscsid_t;
	')

	allow $1 iscsid_t:sem create_sem_perms;
')

########################################
## <summary>
##	Connect to iscsid using a unix
##	domain stream socket.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`iscsi_stream_connect',`
	gen_require(`
		type iscsid_t, iscsi_var_lib_t;
	')

	files_search_var_lib($1)
	stream_connect_pattern($1, iscsi_var_lib_t, iscsi_var_lib_t, iscsid_t)
')

########################################
## <summary>
##	Read iscsid lib files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`iscsi_read_lib_files',`
	gen_require(`
		type iscsi_var_lib_t;
	')

	read_files_pattern($1, iscsi_var_lib_t, iscsi_var_lib_t)
	allow $1 iscsi_var_lib_t:dir list_dir_perms;
	files_search_var_lib($1)
')

########################################
## <summary>
##	Transition to iscsi named content
## </summary>
## <param name="domain">
##	<summary>
##      Domain allowed access.
##	</summary>
## </param>
#
interface(`iscsi_filetrans_named_content',`
    gen_require(`
        type iscsi_lock_t;
    ')

    files_lock_filetrans($1, iscsi_lock_t, dir, "iscsi")
')

########################################
## <summary>
##     Execute iscsi server in the iscsi domain.
## </summary>
## <param name="domain">
##     <summary>
##     Domain allowed to transition.
##     </summary>
## </param>
#
interface(`iscsi_systemctl',`
       gen_require(`
               type iscsid_t;
               type iscsi_unit_file_t;
       ')

       systemd_exec_systemctl($1)
	init_reload_services($1)
       allow $1 iscsi_unit_file_t:file read_file_perms;
       allow $1 iscsi_unit_file_t:service manage_service_perms;

       ps_process_pattern($1, iscsid_t)
')

########################################
## <summary>
##	All of the rules required to
##	administrate an iscsi environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`iscsi_admin',`
	gen_require(`
		type iscsid_t, iscsi_lock_t, iscsi_log_t;
		type iscsi_var_lib_t, iscsi_var_run_t, iscsi_tmp_t;
		type iscsi_unit_file_t;
	')

	allow $1 iscsid_t:process { ptrace signal_perms };
	ps_process_pattern($1, iscsid_t)

	systemd_exec_systemctl($1)
	init_reload_services($1)
	allow $1 iscsi_unit_file_t:file manage_file_perms;
	allow $1 iscsi_unit_file_t:service manage_service_perms;

	logging_search_logs($1)
	admin_pattern($1, iscsi_log_t)

	files_search_locks($1)
	admin_pattern($1, iscsi_lock_t)

	files_search_var_lib($1)
	admin_pattern($1, iscsi_var_lib_t)

	files_search_pids($1)
	admin_pattern($1, iscsi_var_run_t)

	files_search_tmp($1)
	admin_pattern($1, iscsi_tmp_t)
')
