## <summary>full-featured SSL VPN solution.</summary>

########################################
## <summary>
##	Execute openvpn clients in the
##	openvpn domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`openvpn_domtrans',`
	gen_require(`
		type openvpn_t, openvpn_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, openvpn_exec_t, openvpn_t)
')

########################################
## <summary>
##	Execute openvpn clients in the
##	caller domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`openvpn_exec',`
	gen_require(`
		type openvpn_exec_t;
	')

    can_exec($1, openvpn_exec_t)
')

########################################
## <summary>
##	Execute openvpn clients in the
##	openvpn domain, and allow the
##	specified role the openvpn domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`openvpn_run',`
	gen_require(`
		attribute_role openvpn_roles;
	')

	openvpn_domtrans($1)
	roleattribute $2 openvpn_roles;
')

########################################
## <summary>
##	Send kill signals to openvpn.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`openvpn_kill',`
	gen_require(`
		type openvpn_t;
	')

	allow $1 openvpn_t:process sigkill;
')

########################################
## <summary>
##	Send generic signals to openvpn.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`openvpn_signal',`
	gen_require(`
		type openvpn_t;
	')

	allow $1 openvpn_t:process signal;
')

########################################
## <summary>
##	Send null signals to openvpn.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`openvpn_signull',`
	gen_require(`
		type openvpn_t;
	')

	allow $1 openvpn_t:process signull;
')

########################################
## <summary>
##	Read openvpn configuration content.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`openvpn_read_config',`
	gen_require(`
		type openvpn_etc_t;
	')

	files_search_etc($1)
	allow $1 openvpn_etc_t:dir list_dir_perms;
	allow $1 openvpn_etc_t:file read_file_perms;
	allow $1 openvpn_etc_t:lnk_file read_lnk_file_perms;
')

####################################
## <summary>
##  Connect to openvpn over
##	a unix domain stream socket.
## </summary>
## <param name="domain">
##  <summary>
##  Domain allowed access.
##  </summary>
## </param>
#
interface(`openvpn_stream_connect',`
	gen_require(`
		type openvpn_t, openvpn_var_run_t;
	')

	files_search_pids($1)
	stream_connect_pattern($1, openvpn_var_run_t, openvpn_var_run_t, openvpn_t)
')

########################################
## <summary>
##	Read and write to sopenvpn_image devices.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`openvpn_noatsecure',`
	gen_require(`
		type openvpn_t;
	')

    allow $1 openvpn_t:process noatsecure;
')

########################################
## <summary>
##	All of the rules required to
##	administrate an openvpn environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`openvpn_admin',`
	gen_require(`
		type openvpn_t, openvpn_etc_t, openvpn_var_log_t;
		type openvpn_var_run_t, openvpn_initrc_exec_t, openvpn_etc_rw_t;
		type openvpn_status_t;
	')

	allow $1 openvpn_t:process signal_perms;
	ps_process_pattern($1, openvpn_t)

	tunable_policy(`deny_ptrace',`',`
		allow $1 openvpn_t:process ptrace;
	')

	init_labeled_script_domtrans($1, openvpn_initrc_exec_t)
	domain_system_change_exemption($1)
	role_transition $2 openvpn_initrc_exec_t system_r;
	allow $2 system_r;

	files_list_etc($1)
	admin_pattern($1, { openvpn_etc_t openvpn_etc_rw_t })

	logging_list_logs($1)
	admin_pattern($1, { openvpn_status_t openvpn_var_log_t })

	files_list_pids($1)
	admin_pattern($1, openvpn_var_run_t)
')
