## <summary>Port of Apple Rendezvous multicast DNS.</summary>

########################################
## <summary>
##	Send generic signals to howl.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`howl_signal',`
	gen_require(`
		type howl_t;
	')

	allow $1 howl_t:process signal;
')

########################################
## <summary>
##	All of the rules required to
##	administrate an howl environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`howl_admin',`
	gen_require(`
		type howl_t, howl_initrc_exec_t, howl_var_run_t;
	')

	allow $1 howl_t:process { ptrace signal_perms };
	ps_process_pattern($1, howl_t)

	init_labeled_script_domtrans($1, howl_initrc_exec_t)
	domain_system_change_exemption($1)
	role_transition $2 howl_initrc_exec_t system_r;
	allow $2 system_r;

	files_search_pids($1)
	admin_pattern($1, howl_var_run_t)
')
